plugins {
    id 'net.linguica.maven-settings' version '0.5'
    id 'org.ajoberstar.grgit' version '1.6.0'
}

apply plugin: 'com.android.application'

group 'mil.nga.giat.asam'
archivesBaseName = 'asam-android'
version '1.0.2'
ext {
    versionCode = 7
    sourceRefspec = Grgit.open().head().id
}

def googleMapsApiReleaseKey = hasProperty('RELEASE_MAPS_API_KEY') ? RELEASE_MAPS_API_KEY : ''
def googleMapsApiDebugKey = hasProperty('DEBUG_MAPS_API_KEY') ? DEBUG_MAPS_API_KEY : ''
def signedApkPath = hasProperty('SIGNED_APK_PATH') ? SIGNED_APK_PATH : "${project.buildDir}/outputs/apk/${archivesBaseName}-defaults-release.apk"
def signedDebugApkPath = hasProperty('SIGNED_DEBUG_APK_PATH') ? SIGNED_DEBUG_APK_PATH : "${project.buildDir}/outputs/apk/${archivesBaseName}-defaults-debug.apk"

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    dexOptions {
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        applicationId "mil.nga.giat.asam"
        versionName project.version
        versionCode project.versionCode as int
        minSdkVersion 17
        targetSdkVersion 25
        multiDexEnabled true
        resValue "string", "source_refspec", project.sourceRefspec
        resValue "string", "applicationId", applicationId
        manifestPlaceholders source_refspec: project.sourceRefspec
        buildConfigField "String", "SOURCE_REFSPEC", $/"${project.sourceRefspec}"/$
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", "AIzaSyD4ncpeAxwb0KyEMOi4-OFXIWWp61h2uhE"
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiDebugKey
            resValue "string", "applicationId", defaultConfig.applicationId + applicationIdSuffix
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

def configurePom(pom) {
    pom.withXml {
        // for dependencies and exclusions
        def dependenciesNode = asNode().appendNode('dependencies')
        project.configurations.compile.allDependencies.each { Dependency dep ->
            if (dep instanceof SelfResolvingDependency) {
                // apparently the files() dependency results in a SelfResolvingDependency - skip it
                return;
            }

            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', dep.group)
            dependencyNode.appendNode('artifactId', dep.name)
            dependencyNode.appendNode('version', dep.version)

            // for exclusions
            if (dep.excludeRules.size() > 0) {
                def exclusions = dependencyNode.appendNode('exclusions')
                dep.excludeRules.each { ExcludeRule ex ->
                    def exclusion = exclusions.appendNode('exclusion')
                    exclusion.appendNode('groupId', ex.group)
                    exclusion.appendNode('artifactId', ex.module)
                }
            }
        }
    }
}

publishing {
    publications {
        defaultsApk(MavenPublication) {
            artifactId archivesBaseName
            pom.packaging 'apk'
            configurePom(pom)
            artifact "${project.buildDir}/outputs/apk/${archivesBaseName}-defaults-release-unsigned.apk", {
                classifier 'unsigned'
            }
            if (file(signedApkPath).exists()) {
                artifact signedApkPath
            }
            if (file(signedDebugApkPath).exists()) {
                artifact signedDebugApkPath, {
                    classifier 'debug'
                }
            }
            if (project.hasProperty('SKIP_DEBUG_SIGNING')) {
                artifact "${project.buildDir}/outputs/apk/${archivesBaseName}-defaults-debug-unsigned.apk", {
                    classifier 'debug-unsigned'
                }
            }
        }
    }
}

repositories {
    def buildRepo = maven {
        name 'build'
        url project.properties['repositories.build.url'] ?: "${buildDir}/m2"
    }
    publishing.repositories.add buildRepo

    if (project.hasProperty('repositories.shared.name')) {
        def sharedRepo = maven {
            name project.properties['repositories.shared.name']
            url project.properties['repositories.shared.url']
        }
        publishing.repositories.add sharedRepo
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'com.vividsolutions:jts:1.8'
    compile files('libs/poffen_cluster_1.1.jar')
}

task androidArtifactVersion {
    group = 'android'
    description = 'Print the application ID, version code, and version name this build will produce in AndroidManifest.xml'
    doLast {
        com.android.build.gradle.AppExtension appExt = project.extensions.android;
        println "applicationId: ${appExt.defaultConfig.applicationId}"
        println "versionCode: ${appExt.defaultConfig.versionCode}"
        println "versionName: ${appExt.defaultConfig.versionName}"
    }
}

configure extensions.android, {
    if (project.hasProperty('RELEASE_STORE_FILE') && file(project.getProperty('RELEASE_STORE_FILE')).exists()) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
    else {
        println 'no release keystore configured'
    }

    if (project.hasProperty('SKIP_DEBUG_SIGNING')) {
        buildTypes {
            debug {
                signingConfig null
            }
        }
    }
    else if (project.hasProperty('DEBUG_STORE_FILE') && file(project.getProperty('DEBUG_STORE_FILE')).exists()) {
        signingConfigs {
            debug {
                storeFile file(DEBUG_STORE_FILE)
                storePassword DEBUG_STORE_PASSWORD
                keyAlias DEBUG_KEY_ALIAS
                keyPassword DEBUG_KEY_PASSWORD
            }
        }
    }
}

