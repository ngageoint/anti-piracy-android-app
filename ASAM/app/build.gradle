apply plugin: 'com.android.application'
apply plugin: 'maven-publish'
archivesBaseName = 'asam'
version '2.0.1'

def googleMapsApiDebugKey = hasProperty('DEBUG_MAPS_API_KEY') ? DEBUG_MAPS_API_KEY: ''
def googleMapsApiReleaseKey = hasProperty('RELEASE_MAPS_API_KEY') ? RELEASE_MAPS_API_KEY: ''

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "mil.nga.giat.asam"
        versionName project.version
        versionCode VERSION_CODE as int
        minSdkVersion 22
        targetSdkVersion 30
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty('KEYSTORE')) {
                storeFile file(KEYSTORE)
                storePassword KEYSTORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiReleaseKey
        }
        debug {
            minifyEnabled false
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiDebugKey
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

repositories {
    def buildRepo = maven {
        name 'build'
        url project.properties['repositories.build.url'] ?: "${buildDir}/m2"
    }
    publishing.repositories.add buildRepo

    if (project.hasProperty('repositories.shared.name')) {
        def sharedRepo = maven {
            name project.properties['repositories.shared.name']
            url project.properties['repositories.shared.url']
        }
        publishing.repositories.add sharedRepo
    }
    mavenCentral()
}

dependencies {
    api 'androidx.appcompat:appcompat:1.4.1'
    api 'com.google.android.material:material:1.4.0'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.recyclerview:recyclerview:1.2.1'
    api 'com.google.android.gms:play-services-maps:18.0.2'
    api 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    api 'com.vividsolutions:jts:1.13'
    api 'org.apache.commons:commons-lang3:3.4'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation files('libs/poffen_cluster_1.1.jar')
}

